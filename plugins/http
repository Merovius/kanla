#!/usr/bin/env perl
# vim:ts=4:sw=4:expandtab

use strict;
use warnings;
use v5.10;
# libanyevent-http-perl
use AnyEvent::HTTP;
# libanyevent-perl
use AnyEvent;
use AnyEvent::Socket;
use AnyEvent::DNS;
# libjson-xs-perl
use JSON::XS;
# libconfig-general-perl
use Config::General;
# core
use Carp;
use Data::Dumper;
use IO::Handle;

my $errorfh = IO::Handle->new_from_fd(3, 'w');
$errorfh->autoflush(1);

# This code is ripped out of AnyEvent::HTTP. We need to get the hostname from
# an URL to resolve it via IPv4 _and_ IPv6.
sub host_from_url {
    my ($url) = @_;

    my ($uscheme, $uauthority, $upath, $query, undef) = # ignore fragment
        $url =~ m|^([^:]+):(?://([^/?#]*))?([^?#]*)(?:(\?[^#]*))?(?:#(.*))?$|;

    $uauthority =~ /^(?: .*\@ )? ([^\@:]+) (?: : (\d+) )?$/x
        or die "Unparsable URL";
    return lc $1;
}

# reads in the whole STDIN
sub slurp {
    local $/;
    <STDIN>;
}

my $config_str = slurp();
say "config = *$config_str*";

my $conf = Config::General->new(
    -String => $config_str,
    # open all files in utf-8 mode
    -UTF8 => 1,
    # normalize yes, on, 1, true and no, off, 0, false to 1 resp. 0
    -AutoTrue => 1,
    # case-insensitive key names by lowercasing everything
    -LowerCaseNames => 1,
    # provide the ->array, ->hash, etc. methods
    -ExtendedAccess => 1,
    # we need to merge duplicate options,
    # otherwise the default config
    # will not get overwritten,
    # but amended.
    -MergeDuplicateOptions => 1,
    -FlagBits => {
        family => {
            ipv4 => 1,
            ipv6 => 1,
        },
    },
    -DefaultConfig => <<'EOT'
family = ipv4 | ipv6
EOT
);

# TODO: support multiple URLs
my $url = $conf->value('url');
my $interval = 60;

# Disable buffering.
$| = 1;

sub signal_error {
    my ($severity, $message) = @_;
    say $errorfh encode_json({
        severity => $severity,
        message => $message
    });
}

sub verify_availability {
    my ($ip) = @_;
    http_get $url,
        # This timeout of 10s is for each individual stage, e.g. for
        # connecting, for waiting for a response, etc. It is not a
        # global timeout.
        timeout => 10,
        headers => {
            # TODO: configurable User-Agent
            'User-Agent' => 'kanla',
        },
        tcp_connect => sub {
            my (undef, $port, $connect_cb, $prepare_cb) = @_;
            # Wrap around tcp_connect, replacing the host with our resolved one.
            AnyEvent::Socket::tcp_connect($ip, $port, $connect_cb, $prepare_cb);
        },
        sub {
            my ($body, $hdr) = @_;
# TODO: better check for a non-error instead of 2xx since 3xx (redirects) might also be okay
            if ($hdr->{Status} !~ /^2/) {
                signal_error('critical', 'HTTP reply ' . $hdr->{Status} . " for $url ($ip)");
                return;
            }
            if ($body !~ /Download the latest version/) {
                signal_error('critical', "HTTP body of $url ($ip) does not match template");
                return;
            }
        };
}

sub resolved {
    # family is either A or AAAA
    my $family = shift;
    if (@_ == 0) {
        # TODO: include the hostname
        signal_error('critical', "Cannot resolve $family DNS record");
        return;
    }
    for my $ip (@_) {
        verify_availability($ip);
    }
}

# Periodically run the check, but donâ€™t wait for the first $interval seconds to
# pass, but run it right now, too.
my $main_timer;
$main_timer = AnyEvent->timer(
    after => 0,
    interval => $interval,
    cb => sub {
        # XXX: We re-resolve all the time because it is assumed that you are
        # using a local caching nameserver, which is a good idea anyways and
        # allows us to correctly pick up new addresses once the old ones
        # expire.

        # Since AnyEvent::HTTP offers no way to specify whether we want to
        # access the website via IPv4 or IPv6, we need to resolve the hostname
        # on our own and specifically connect to the resolved IP address.
        my $host = host_from_url($url);
        if ($conf->obj('family')->value('ipv4')) {
            AnyEvent::DNS::a    $host, sub { resolved('A', @_) };
        }
        if ($conf->obj('family')->value('ipv6')) {
            AnyEvent::DNS::aaaa $host, sub { resolved('AAAA', @_) };
        }
    });

# Run forever.
AnyEvent->condvar->recv;
